_kernpkgver_i686='4.8.8-2'
_kernpkgver_x86_64='4.8.8-2'
_kernpkgver=${_kernpkgver_x86_64}
[ "${CARCH}" == "i686" ] && _kernpkgver=${_kernpkgver_i686}
_kernver="${_kernpkgver%-*}"
_kernpkgrel="${_kernpkgver##*-}"

pkgname=('zfs-utils-git')
pkgver=0.7.0.rc2.r42.g7ca2505_4.8.8r2
pkgrel=1
pkgdesc='Zettabyte File System support files'
arch=('i686' 'x86_64')
url='http://zfsonlinux.org/'
license=('CDDL')
groups=('archzfs-git')
depends=('spl-git')
optdepends=(
    'python: arcstat.py, arc_summary.py and dbufstat.py scripts support'
)
makedepends=('git')
provides=('zfs-utils')
conflicts=('zfs-utils')
backup=(
    'etc/zfs/zed.d/zed.rc'
)
source=(
    'git+https://github.com/zfsonlinux/zfs.git'
    'zfs-utils.bash-completion'
    'zfs-utils.initcpio.install'
    'zfs-utils.initcpio.hook'
)
md5sums=(
    'SKIP'
    'c6339c85b1918ad9e3d952d0190c0960'
    'a01aa97b8a8f155803bb0a760286dd4d'
    '4bc6c1de6fe3aa6d1ea178f29ba36f80'
)

pkgver() {
    cd "${srcdir}/zfs"
    # cutting off 'zfs-' prefix that presents in the git tag
    echo $(git describe --long | sed -r \
        's/^zfs-//;s/([^-]*-g)/r\1/;s/-/./g')_${_kernver}r${_kernpkgrel}
}

build() {
    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
                --sbindir=/usr/bin \
                --libexecdir=/usr/lib \
                --with-mounthelperdir=/usr/bin \
                --sysconfdir=/etc \
                --with-udevdir=/usr/lib/udev \
                --with-config=user
    make
}

package() {
    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install

    rm -rf "${pkgdir}/etc/init.d"
    rm -rf "${pkgdir}/usr/lib/dracut"

    install -D -m 0644 "${srcdir}/zfs-utils.initcpio.hook" \
        "${pkgdir}/usr/lib/initcpio/hooks/zfs"
    install -D -m 0644 "${srcdir}/zfs-utils.initcpio.install" \
        "${pkgdir}/usr/lib/initcpio/install/zfs"
    install -D -m 0644 "${srcdir}/zfs-utils.bash-completion" \
        "${pkgdir}/usr/share/bash-completion/completions/zfs"
}

# vim:set ts=4 sw=4 et:
