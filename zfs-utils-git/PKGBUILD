_kernpkgver='4.16.13-2'
_kernver="${_kernpkgver%-*}"
_kernpkgrel="${_kernpkgver##*-}"

pkgname=('zfs-utils-git')
pkgver=0.7.0.r1604.g29445fe3a_4.16.13r2
pkgrel=1
pkgdesc='Zettabyte File System support files'
arch=('x86_64')
url='http://zfsonlinux.org/'
license=('CDDL')
groups=('archzfs-git')
optdepends=(
    'python: arcstat.py, arc_summary.py and dbufstat.py scripts support'
)
makedepends=(
    'git'
)
provides=(
    'zfs-utils'
    'spl-utils'
)
conflicts=(
    'zfs-utils'
    'spl-utils'
)
replaces=(
    'spl-utils'
)
backup=(
    'etc/zfs/zed.d/zed.rc'
)
source=(
    'git+https://github.com/zfsonlinux/zfs.git'
    'zfs-utils.bash-completion'
    'zfs-utils.initcpio.hook'
    'zfs-utils.initcpio.install'
    'hostid.default'
)
sha512sums=(
    'SKIP'
    '6897955ea9b543e52fc99fd04f81f0c893ce26bb70a56c1029dd7cb46047d61fdc11abeb7daa681d50deb2f9854bb194385830656babb685db2c450d9956c892'
    '6d7082506a3a1253cfd0e0cb8089062b1716f9969d4c71f2098da07d14a81c58313f90a393091cdcfbaef0755141f66e9f4670496b39fa08da96a53ddffc6f0d'
    '27a3b51048c79a0ee2c43a79a9714fc2cf9a2d350c256c43678c06accdcfd50d1adc965e8ffa7eeb6616c2901bf314bfeba363b378f98d6bb3245863b41d63a4'
    'ea71bb243b0b2db729b9eb88e3c55a3f490fbff23457825051224a1fe6e6d3f480590cfa3a4a6b12c622d6ac366feb03cd17004ed004cb3f0d52731626946679'
)

pkgver() {
    cd "${srcdir}/zfs"
    # cutting off 'zfs-' prefix that presents in the git tag
    echo $(git describe --long | sed -r \
        's/^zfs-//;s/([^-]*-g)/r\1/;s/-/./g')_${_kernver}r${_kernpkgrel}
}

build() {
    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
                --sbindir=/usr/bin \
                --libexecdir=/usr/lib \
                --with-mounthelperdir=/usr/bin \
                --sysconfdir=/etc \
                --with-udevdir=/usr/lib/udev \
                --with-config=user
    make
}

package() {
    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install

    chmod 0750 "${pkgdir}/etc/sudoers.d"

    rm -rf "${pkgdir}/etc/init.d"
    rm -rf "${pkgdir}/usr/lib/dracut"

    install -D -m 0644 "${srcdir}/zfs-utils.initcpio.hook" \
        "${pkgdir}/usr/lib/initcpio/hooks/zfs"
    install -D -m 0644 "${srcdir}/zfs-utils.initcpio.install" \
        "${pkgdir}/usr/lib/initcpio/install/zfs"
    install -D -m 0644 "${srcdir}/zfs-utils.bash-completion" \
        "${pkgdir}/usr/share/bash-completion/completions/zfs"
    install -D -m 0644 "${srcdir}/hostid.default" \
        "${pkgdir}/etc/hostid"
}

# vim: set ts=4 sts=4 sw=4 et:
