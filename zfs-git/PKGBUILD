_kernel_package_version='5.3.10.1-1'
_kernel_package_release="${_kernel_package_version##*-}"
_kernel_archlinux_version="${_kernel_package_version%-*}"
_kernel_archlinux_minor="${_kernel_archlinux_version##*.}"
_kernel_base_version="${_kernel_archlinux_version%.*}"

pkgname=('zfs-git')
pkgver=0.8.0.r385.g6c7023a53_5.3.10.1.r1
pkgrel=1
pkgdesc='Zettabyte File System kernel modules'
arch=('x86_64')
url='http://zfsonlinux.org/'
license=('CDDL')
groups=('archzfs-git')
depends=(
    'zfs-utils-git'
    "linux=${_kernel_package_version}"
)
makedepends=(
    'git'
    "linux-headers=${_kernel_package_version}"
    'python'
)
provides=(
    'zfs'
    'spl'
)
conflicts=(
    'zfs'
    'spl'
)
replaces=(
    'spl'
)
install='zfs.install'
source=(
    'git+https://github.com/zfsonlinux/zfs.git'
)
sha512sums=(
    'SKIP'
)

pkgver() {
    cd "${srcdir}/zfs"
    # cutting off 'zfs-' prefix that presents in the git tag
    _zfs_version="$(git describe --long | sed -r 's/^zfs-//;s/([^-]*-g)/r\1/;s/-/./g')"
    echo "${_zfs_version}_${_kernel_archlinux_version}.r${_kernel_package_release}"
}

build() {
    _kernel_build_path="/usr/lib/modules/${_kernel_base_version}-arch${_kernel_archlinux_minor}-"
    _kernel_build_path+="${_kernel_package_release}/build"
    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
                --with-config=kernel \
                --with-linux=${_kernel_build_path}
    make
}

package() {
    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install
    mv "${pkgdir}/lib" "${pkgdir}/usr/"
    sed -i "s|${srcdir}||" "${pkgdir}"/usr/src/zfs-*/*/Module.symvers
}

# vim: set ts=4 sts=4 sw=4 tw=100 et ft=sh:
