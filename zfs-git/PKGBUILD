_kernpkgver='4.18.12.arch1-1'
_kernver="${_kernpkgver%-*}"
_kernpkgrel="${_kernpkgver##*-}"
_kernfullpkgver=$( echo ${_kernpkgver} | \
    sed 's|\(^[[:digit:]]\+\.[[:digit:]]\+\.[[:digit:]]\+\)\.\(arch[[:digit:]]\+-[[:digit:]]\+$\)|\1-\2|' )

pkgname=('zfs-git')
pkgver=0.8.0.rc1.r38.gdfbe26750_4.18.12.arch1.r1
pkgrel=1
pkgdesc='Zettabyte File System kernel modules'
arch=('x86_64')
url='http://zfsonlinux.org/'
license=('CDDL')
groups=('archzfs-git')
depends=(
    'zfs-utils-git'
    "linux=${_kernpkgver}"
)
makedepends=(
    'git'
    "linux-headers=${_kernpkgver}"
)
provides=(
    'zfs'
    'spl'
)
conflicts=(
    'zfs'
    'spl'
)
replaces=(
    'spl'
)
install='zfs.install'
source=(
    'git+https://github.com/zfsonlinux/zfs.git'
)
sha512sums=(
    'SKIP'
)

pkgver() {
    cd "${srcdir}/zfs"
    # cutting off 'zfs-' prefix that presents in the git tag
    echo $(git describe --long | sed -r \
        's/^zfs-//;s/([^-]*-g)/r\1/;s/-/./g')_${_kernver}.r${_kernpkgrel}
}

build() {
    _kernbuildpath="/usr/lib/modules/${_kernfullpkgver}-ARCH/build"
    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
                --with-config=kernel \
                --with-linux=${_kernbuildpath}
    make
}

package() {
    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install
    mv "${pkgdir}/lib" "${pkgdir}/usr/"
    sed -i "s+${srcdir}++" \
        "${pkgdir}"/usr/src/{spl,zfs}-*/${_kernfullpkgver}-ARCH/Module.symvers
}

# vim: set ts=4 sts=4 sw=4 et:
