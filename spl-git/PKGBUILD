_kernpkgver='4.16.7-1'
_kernver="${_kernpkgver%-*}"
_kernpkgrel="${_kernpkgver##*-}"
_kernfullpkgver=$( echo ${_kernpkgver} | \
    sed 's/\(^[[:digit:]]\.[[:digit:]]\+\)\(-[[:digit:]]\+$\)/\1.0\2/' )

pkgname=('spl-git')
pkgver=0.7.0.r35.g84a80d5_4.16.7r1
pkgrel=1
pkgdesc='Solaris Porting Layer kernel modules'
arch=('x86_64')
url='http://zfsonlinux.org/'
license=('GPL')
groups=('archzfs-git')
depends=(
    'spl-utils-git'
    "linux=${_kernpkgver}"
)
makedepends=(
    'git'
    "linux-headers=${_kernpkgver}"
)
provides=('spl')
conflicts=('spl')
install='spl.install'
source=('git+https://github.com/zfsonlinux/spl.git')
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/spl"
    # cutting off 'spl-' prefix that presents in the git tag
    echo $(git describe --long | sed -r \
        's/^spl-//;s/([^-]*-g)/r\1/;s/-/./g')_${_kernver}r${_kernpkgrel}
}

build() {
    _kernbuildpath="/usr/lib/modules/${_kernfullpkgver}-ARCH/build"

    cd "${srcdir}/spl"
    ./autogen.sh
    ./configure --prefix=/usr \
                --with-config=kernel \
                --with-linux=${_kernbuildpath}
    make
}

package() {
    cd "${srcdir}/spl"
    make DESTDIR="${pkgdir}" install
    mv "${pkgdir}/lib" "${pkgdir}/usr/"
    sed -i "s+${srcdir}++" \
        "${pkgdir}"/usr/src/spl-*/${_kernfullpkgver}-ARCH/Module.symvers
}

# vim:set ts=4 sw=4 et:
