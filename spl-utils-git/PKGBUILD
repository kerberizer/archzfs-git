_kernpkgver_i686='4.7.6-1'
_kernpkgver_x86_64='4.7.6-1'
_kernpkgver=${_kernpkgver_x86_64}
[ "${CARCH}" == "i686" ] && _kernpkgver=${_kernpkgver_i686}
_kernver="${_kernpkgver%-*}"
_kernpkgrel="${_kernpkgver##*-}"

pkgname=('spl-utils-git')
pkgver=0.7.0.rc1.r5.g6c2a66b_4.7.6r1
pkgrel=1
pkgdesc='Solaris Porting Layer support files'
arch=('i686' 'x86_64')
url='http://zfsonlinux.org/'
license=('GPL')
groups=('archzfs-git')
depends=('glibc')
makedepends=('git')
provides=('spl-utils')
conflicts=('spl-utils')
source=(
    'git+https://github.com/zfsonlinux/spl.git'
    'spl-utils.hostid'
)
md5sums=(
    'SKIP'
    'a54f0041a9e15b050f25c463f1db7449'
)

pkgver() {
    cd "${srcdir}/spl"
    # cutting off 'spl-' prefix that presents in the git tag
    echo $(git describe --long | sed -r \
        's/^spl-//;s/([^-]*-g)/r\1/;s/-/./g')_${_kernver}r${_kernpkgrel}
}

build() {
    cd "${srcdir}/spl"
    ./autogen.sh
    ./configure --prefix=/usr \
                --sbindir=/usr/bin \
                --with-config=user
    make
}

package() {
    cd "${srcdir}/spl"
    make DESTDIR="${pkgdir}" install
    install -D -m 0644 "${srcdir}/spl-utils.hostid" "${pkgdir}/etc/hostid"
}

# vim:set ts=4 sw=4 et:
