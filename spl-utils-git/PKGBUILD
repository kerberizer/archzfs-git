_kernpkgver_i686='3.14-5'
_kernpkgver_x86_64='3.14-5'
_kernpkgver=${_kernpkgver_x86_64}
[ "${CARCH}" == "i686" ] && _kernpkgver=${_kernpkgver_i686}
_kernver="${_kernpkgver%-*}"

pkgname=('spl-utils-git')
pkgver=0.6.2.r28.g3ceb71e_3.14
pkgrel=1
pkgdesc=('Solaris Porting Layer support files')
arch=('i686' 'x86_64')
url='http://zfsonlinux.org/'
license=('GPL')
groups=('archzfs-git')
makedepends=('git')
provides=('spl-utils')
conflicts=('spl-utils')
source=('git+https://github.com/zfsonlinux/spl.git'
        'spl-utils.hostid')
md5sums=('SKIP'
         'a54f0041a9e15b050f25c463f1db7449')

pkgver() {
  cd "${srcdir}/spl"
  # cutting off 'spl-' prefix that presents in the git tag
  echo $(git describe --long | \
    sed -r 's/^spl-//;s/([^-]*-g)/r\1/;s/-/./g')_${_kernver}
}

build() {
  cd "${srcdir}/spl"
  ./autogen.sh
  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --with-config=user
  make
}

package() {
  cd "${srcdir}/spl"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/spl-utils.hostid" "${pkgdir}/etc/hostid"
}

# vim:set ts=2 sw=2 et:
