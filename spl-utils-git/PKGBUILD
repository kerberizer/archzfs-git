_kernpkgver='4.15.6-1'
_kernver="${_kernpkgver%-*}"
_kernpkgrel="${_kernpkgver##*-}"

pkgname=('spl-utils-git')
pkgver=0.7.0.r30.g3673d03_4.15.6r1
pkgrel=1
pkgdesc='Solaris Porting Layer support files'
arch=('x86_64')
url='http://zfsonlinux.org/'
license=('GPL')
groups=('archzfs-git')
depends=('glibc')
makedepends=('git')
provides=('spl-utils')
conflicts=('spl-utils')
source=(
    'git+https://github.com/zfsonlinux/spl.git'
    'spl-utils.hostid'
)
sha512sums=(
    'SKIP'
    'ea71bb243b0b2db729b9eb88e3c55a3f490fbff23457825051224a1fe6e6d3f480590cfa3a4a6b12c622d6ac366feb03cd17004ed004cb3f0d52731626946679'
)

pkgver() {
    cd "${srcdir}/spl"
    # cutting off 'spl-' prefix that presents in the git tag
    echo $(git describe --long | sed -r \
        's/^spl-//;s/([^-]*-g)/r\1/;s/-/./g')_${_kernver}r${_kernpkgrel}
}

build() {
    cd "${srcdir}/spl"
    ./autogen.sh
    ./configure --prefix=/usr \
                --sbindir=/usr/bin \
                --with-config=user
    make
}

package() {
    cd "${srcdir}/spl"
    make DESTDIR="${pkgdir}" install
    install -D -m 0644 "${srcdir}/spl-utils.hostid" "${pkgdir}/etc/hostid"
}

# vim:set ts=4 sw=4 et:
